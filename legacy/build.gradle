plugins {
    id 'java'
}

group 'io.github.krlvm.powertunnel.plugins'
version '1.0'

repositories {
    mavenCentral()
}

configurations {
    fatImplementation
    compile.extendsFrom(fatImplementation)
}

task fatJar(type: Jar, group: 'build', description: 'Assembles a jar archive containing the main classes and all dependencies.') {
    archiveClassifier = 'all'
    with jar
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    from {
        configurations.fatImplementation.asFileTree.files.collect { zipTree(it) }
    } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}

dependencies {
    implementation('io.github.krlvm.powertunnel:sdk') {
        version {
            branch = 'master'
        }
    }

    // ToDo: Linux support
    implementation files(System.getenv('LocalAppData') + '\\Android\\Sdk\\platforms\\android-30\\android.jar')

    fatImplementation 'dnsjava:dnsjava:2.1.9'
    fatImplementation 'org.jitsi:dnssecjava:1.2.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}